## <summary>Policy for mount.</summary>

########################################
## <summary>
##	Execute mount in the mount domain.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
#
interface(`mount_domtrans',`
	gen_require(`
		type mount_t, mount_exec_t;
	')

	domtrans_pattern($1,mount_exec_t,mount_t)
')

########################################
## <summary>
##	Execute mount in the mount domain, and
##	allow the specified role the mount domain,
##	and use the caller's terminal.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the mount domain.
##	</summary>
## </param>
## <param name="terminal">
##	<summary>
##	The type of the terminal allow the mount domain to use.
##	</summary>
## </param>
## <rolecap/>
#
interface(`mount_run',`
	gen_require(`
		type mount_t;
	')

	mount_domtrans($1)
	role $2 types mount_t;
	allow mount_t $3:chr_file rw_file_perms;

	optional_policy(`
		samba_run_smbmount($1, $2, $3)
	')
')

########################################
## <summary>
##	Execute mount in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
#
interface(`mount_exec',`
	gen_require(`
		type mount_exec_t;
	')

	# cjp: this should be removed:
	allow $1 mount_exec_t:dir list_dir_perms;

	allow $1 mount_exec_t:lnk_file read_lnk_file_perms;
	can_exec($1,mount_exec_t)
')

########################################
## <summary>
##	Use file descriptors for mount.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
#
interface(`mount_use_fds',`
	gen_require(`
		type mount_t;
	')

	allow $1 mount_t:fd use; 
')

########################################
## <summary>
##	Allow the mount domain to send nfs requests for mounting
##	network drives
## </summary>
## <desc>
##	<p>
##	Allow the mount domain to send nfs requests for mounting
##	network drives
##	</p>
##	<p>
##	This interface has been deprecated as these rules were
##	a side effect of leaked mount file descriptors.  This
##	interface has no effect.
##	</p>
## </desc>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`mount_send_nfs_client_request',`
	refpolicywarn(`$0($*) has been deprecated.')
')

########################################
## <summary>
##	Execute mount in the unconfined mount domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`mount_domtrans_unconfined',`
	gen_require(`
		type unconfined_mount_t, mount_exec_t;
	')

	domtrans_pattern($1, mount_exec_t, unconfined_mount_t)
')

########################################
## <summary>
##	Execute mount in the unconfined mount domain, and
##	allow the specified role the unconfined mount domain,
##	and use the caller's terminal.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the unconfined mount domain.
##	</summary>
## </param>
## <param name="terminal">
##	<summary>
##	The type of the terminal allow the unconfined mount domain to use.
##	</summary>
## </param>
## <rolecap/>
#
interface(`mount_run_unconfined',`
	gen_require(`
		type unconfined_mount_t;
	')

	mount_domtrans_unconfined($1)
	role $2 types unconfined_mount_t;
	allow unconfined_mount_t $3:chr_file rw_file_perms;
')
