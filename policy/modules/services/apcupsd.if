## <summary>APC UPS monitoring daemon</summary>

########################################
## <summary>
##	Execute a domain transition to run apcupsd.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`apcupsd_domtrans',`
	gen_require(`
		type apcupsd_t, apcupsd_exec_t;
	')

	domtrans_pattern($1,apcupsd_exec_t,apcupsd_t)
')

########################################
## <summary>
##	Read apcupsd PID files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`apcupsd_read_pid_files',`
	gen_require(`
		type apcupsd_var_run_t;
	')

	files_search_pids($1)
	allow $1 apcupsd_var_run_t:file read_file_perms;
')


########################################
## <summary>
##	Allow the specified domain to read apcupsd's log files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`apcupsd_read_log',`
	gen_require(`
		type apcupsd_log_t;
	')

	logging_search_logs($1)
	allow $1 apcupsd_log_t:dir list_dir_perms;
	allow $1 apcupsd_log_t:file { read getattr lock };
')

########################################
## <summary>
##	Allow the specified domain to append
##	apcupsd log files.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed to transition.
## 	</summary>
## </param>
#
interface(`apcupsd_append_log',`
	gen_require(`
		type var_log_t, apcupsd_log_t;
	')

	logging_search_logs($1)
	allow $1 apcupsd_log_t:dir list_dir_perms;
	allow $1 apcupsd_log_t:file { getattr append };
')

########################################
## <summary>
##	Execute a domain transition to run httpd_apcupsd_cgi_script.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`httpd_apcupsd_cgi_script_domtrans',`
	gen_require(`
		type httpd_apcupsd_cgi_script_t, httpd_apcupsd_cgi_script_exec_t;
	')

	domtrans_pattern($1,httpd_apcupsd_cgi_script_exec_t,httpd_apcupsd_cgi_script_t)
')
