## <summary>TOR, the onion router</summary>

########################################
## <summary>
##	Execute a domain transition to run TOR.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`tor_domtrans',`
	gen_require(`
		type tor_t, tor_exec_t;
	')

	domtrans_pattern($1,tor_exec_t,tor_t)
')

########################################
## <summary>
##	All of the rules required to administrate 
##	an tor environment
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`tor_admin',`
	gen_require(`
		type tor_t, tor_var_log_t, tor_etc_t;
		type tor_var_lib_t, tor_var_run_t;
	')

	allow $1 tor_t:process { ptrace signal_perms getattr };
	ps_process_pattern($1, tor_t)
	        
	logging_list_logs($1)
	manage_files_pattern($1, tor_var_log_t, tor_var_log_t)

	files_list_etc($1)
	manage_files_pattern($1, tor_etc_t, tor_etc_t)

	files_list_var_lib($1)
	manage_files_pattern($1, tor_var_lib_t, tor_var_lib_t)

	files_list_pids($1)
	manage_files_pattern($1, tor_var_run_t, tor_var_run_t)
')
