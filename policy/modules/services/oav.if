## <summary>Open AntiVirus scannerdaemon and signature update</summary>

########################################
## <summary>
##	Execute oav_update in the oav_update domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`oav_domtrans_update',`
	gen_require(`
		type oav_update_t, oav_update_exec_t;
	')

	corecmd_search_sbin($1)
	domain_auto_trans($1,oav_update_exec_t,oav_update_t)

	allow $1 oav_update_t:fd use;
	allow oav_update_t $1:fd use;
	allow oav_update_t $1:fifo_file rw_file_perms;
	allow oav_update_t $1:process sigchld;
')

########################################
## <summary>
##	Execute oav_update in the oav_update domain, and
##	allow the specified role the oav_update domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the oav_update domain.
##	</summary>
## </param>
## <param name="terminal">
##	<summary>
##	The type of the terminal allow the oav_update domain to use.
##	</summary>
## </param>
#
interface(`oav_run_update',`
	gen_require(`
		type oav_update_t;
	')

	oav_domtrans_update($1)
	role $2 types oav_update_t;
	allow oav_update_t $3:chr_file rw_term_perms;
')
