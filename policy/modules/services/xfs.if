## <summary>X Windows Font Server </summary>

########################################
## <summary>
##	Read a X font server named socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`xfs_read_sockets',`
	gen_require(`
		type xfs_tmp_t;
	')

	files_search_tmp($1)
	allow $1 xfs_tmp_t:dir search;
	allow $1 xfs_tmp_t:sock_file { getattr read };
')

########################################
## <summary>
##	Connect to a X font server over
##	a unix domain stream socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`xfs_stream_connect',`
	gen_require(`
		type xfs_tmp_t, xfs_t;
	')

	files_search_tmp($1)
	allow $1 xfs_tmp_t:dir search;
	allow $1 xfs_tmp_t:sock_file write;
	allow $1 xfs_t:unix_stream_socket connectto;
')


########################################
## <summary>
##	Allow the specified domain to execute xfs
##	in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`xfs_exec',`
	gen_require(`
		type xfs_exec_t;
	')

	can_exec($1,xfs_exec_t)
')
